#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/feature/ten_package.gni")
import("//build/ten_common/general/glob.gni")
import("//build/ten_common/rust/rust.gni")
import("//build/ten_manager/options.gni")

declare_args() {
  use_shared_lib = false
}

copy_glob("ten_manager_standalone_test_data") {
  dest_root_dir = "${root_out_dir}/tests/standalone/ten_manager"

  resources = []

  test_data_files =
      exec_script("//.gnfiles/build/scripts/glob_file.py",
                  [
                    "--dir",
                    rebase_path("//core/src/ten_manager/tests/test_data/**/*"),
                    "--dir-base",
                    rebase_path("//core/src/ten_manager/tests/test_data"),
                    "--recursive",
                    "--only-output-file",
                  ],
                  "json")

  foreach(test_data_file, test_data_files) {
    test_data_file_rel_path = test_data_file.relative_path
    resources += [ "//core/src/ten_manager/tests/test_data/${test_data_file_rel_path}=>tests/test_data/${test_data_file_rel_path}" ]
  }
}

if (ten_enable_ten_manager) {
  if (ten_manager_enable_tests) {
    rust_test("tman_test_cargo_build") {
      project_path = "//core/src/ten_manager"
      integration_test_output_name = "integration_test"

      # The transitive dependency of the ten_rust crate.
      utils_static_lib_path = rebase_path("${root_gen_dir}/core/src/ten_utils")
      env = [ "TEN_UTILS_LIBRARY_PATH=${utils_static_lib_path}" ]

      # If the output directory of `cargo build` (tman) and
      # `cargo build --tests` (tman_test) is the same, and if `cargo build` is
      # executed first followed by `cargo build --tests`, the resulting
      # executable will be the normal executable, not the test case executable.
      # Therefore, it's necessary to separate the `target-dir` for these two
      # `cargo build` commands to avoid interfering with each other.
      target_path = "${root_gen_dir}/core/src/ten_manager_test"

      # Copy the test executable to the test output directory.
      test_output_dir = "${root_out_dir}/tests/standalone/ten_manager"

      deps = [
        ":ten_manager_standalone_test_data",
        "//core/src/ten_rust:ten_rust_static_lib",
      ]
    }

    if (ten_enable_cargo_clean) {
      rust_clean("tman_test_cargo_clean") {
        project_path = "//core/src/ten_manager"
        target_path = "${root_gen_dir}/core/src/ten_manager_test"

        # Build tman_test first, then `cargo clean` it.
        deps = [ ":tman_test_cargo_build" ]
      }
    }

    group("tman_test") {
      if (ten_enable_cargo_clean) {
        deps = [ ":tman_test_cargo_clean" ]
      } else {
        deps = [ ":tman_test_cargo_build" ]
      }
    }
  }

  if (ten_manager_enable_frontend) {
    action("build_designer_frontend") {
      script = "//build/ten_manager/scripts/build_frontend.py"

      frontend_files = exec_script(
              "//.gnfiles/build/scripts/glob_file.py",
              [
                "--dir",
                rebase_path(
                    "//core/src/ten_manager/designer_frontend/src/**/*.tsx"),
                "--dir",
                rebase_path(
                    "//core/src/ten_manager/designer_frontend/src/**/*.ts"),
                "--dir",
                rebase_path(
                    "//core/src/ten_manager/designer_frontend/src/**/*.css"),
                "--dir",
                rebase_path("//core/src/ten_manager/designer_frontend/*.json"),
                "--dir",
                rebase_path("//core/src/ten_manager/designer_frontend/*.js"),
                "--dir",
                rebase_path("//core/src/ten_manager/designer_frontend/*.ts"),
                "--dir",
                rebase_path(
                    "//core/src/ten_manager/designer_frontend/index.html"),
                "--recursive",
              ],
              "json")

      sources = []
      foreach(frontend_file, frontend_files) {
        sources += [ frontend_file.path ]
      }

      args = [
        "--dir",
        rebase_path("//core/src/ten_manager/designer_frontend"),
        "--log-level",
        "${log_level}",
      ]

      # A timestamp file is used to track the last build time of the frontend.
      outputs = [ "${root_out_dir}/gen/tman_frontend_build.timestamp" ]
    }
  } else {
    # Because tman will look for `designer_frontend/dist/` during compilation to
    # bundle the frontend codes, even if `ten_manager_enable_frontend=false`, an
    # empty `designer_frontend/dist/` is still required to prevent `tman` from
    # encountering compilation errors.
    action("create_frontend_dist_folder") {
      script = "//build/ten_common/scripts/mkdir.py"

      args = [ rebase_path("//core/src/ten_manager/designer_frontend/dist/") ]

      outputs = [ "${root_out_dir}/gen/core/src/ten_manager/designer_frontend/dist/.placeholder" ]
    }
  }

  rust_target("tman") {
    project_path = "//core/src/ten_manager"
    manifest_path = "//core/src/ten_manager/Cargo.toml"
    target_path = "${target_gen_dir}/tman"

    # The transitive dependency of the ten_rust crate.
    utils_static_lib_path = rebase_path("${root_gen_dir}/core/src/ten_utils")
    env = [ "TEN_UTILS_LIBRARY_PATH=${utils_static_lib_path}" ]

    extra_sources = [
      "//core/src/ten_manager/src/solver/display.lp",
      "//core/src/ten_manager/src/solver/main.lp",
    ]

    if (is_win) {
      output_name = "tman.exe"
    } else {
      output_name = "tman"
    }

    deps = [
      "//core/src/ten_rust:ten_rust_static_lib",
    ]

    if (ten_manager_enable_frontend) {
      deps += [
        # The frontend build is required before building the backend.
        ":build_designer_frontend",
      ]
    } else {
      deps += [
        # The `designer_frontend/dist/` is required before building the backend.
        ":create_frontend_dist_folder",
      ]
    }
  }

  # Assembling the final ten manager package.
  ten_package("ten_manager_package") {
    package_kind = "custom"
    package_output_root_dir_name = "ten_manager"

    tman_output = get_target_outputs(":tman")

    resources = []
    if (is_win) {
      resources += [ "${tman_output[0]}=>bin/tman.exe" ]
    } else {
      resources += [ "${tman_output[0]}=>bin/tman" ]
    }

    deps = [
      ":tman",
    ]
  }

  if (ten_enable_cargo_clean) {
    rust_clean("tman_cargo_clean") {
      project_path = "//core/src/ten_manager"
      tman_target_gen_dir = get_label_info(":tman", "target_gen_dir")
      target_path = "${tman_target_gen_dir}/tman"

      # Build tman first, then `cargo clean` it.
      deps = [ ":ten_manager_package" ]
    }
  }

  group("ten_manager") {
    if (ten_enable_cargo_clean) {
      deps = [ ":tman_cargo_clean" ]
    } else {
      deps = [ ":ten_manager_package" ]
    }
  }
}
